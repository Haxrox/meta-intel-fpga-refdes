#@TYPE: Machine
#@NAME: Intel SoCFPGA Arria10
#@DESCRIPTION: Machine configuration for the Arria 10 SoCFPGA

# Usage:
# Add the line below into conf/local.conf to enable additional
# settings for Arria10 SoCFPGA:
# require conf/machine/arria10-extra.conf

KERNEL_DEVICETREE = "\
		socfpga_arria10_socdk_sdmmc.dtb \
		${@bb.utils.contains("IMAGE_TYPE", "nand", "socfpga_arria10_socdk_nand.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "qspi", "socfpga_arria10_socdk_qspi.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "fpga_static_region.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "persona0.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "persona1.dtb", "", d)} \
		"

EXTRA_IMAGEDEPENDS += "\
		${@bb.utils.contains("IMAGE_TYPE", "pr", " u-boot-socfpga-scr", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "", " hw-ref-design", d)} \
		"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "\
		${@bb.utils.contains("IMAGE_TYPE", "pr", " kernel-devicetree", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", " hw-ref-design", "", d)} \
		"

IMAGE_BOOT_FILES = "\
		${@bb.utils.contains("IMAGE_TYPE", "gsrd", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pcie", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "sgmii", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "tse", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		fit_spl_fpga_${IMAGE_TYPE}.itb;fit_spl_fpga.itb \
		${KERNEL_IMAGETYPE} \
		${UBOOT_BINARY} \
		extlinux.conf;extlinux/extlinux.conf \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "boot.scr", "", d)} \
		"
